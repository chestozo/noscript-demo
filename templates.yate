module "main"

include "node_modules/noscript/yate/noscript.yate"

// ----------------------------------------------------------------------------------------------------------------- //

// Рендеринг всей страницы на сервере.
match / server-render {
    html('<!DOCTYPE html>')
    <html>
    <head>
        <title>demo</title>
        <meta charset="utf-8"></meta>
        <meta http-equiv="X-UA-Compatible" content="IE=EmulateIE7,IE=edge"></meta>
        <link rel="stylesheet" href="/css/all.css"></link>
        <script src="/js/jquery.min.js"></script>
        <script src="/node_modules/es5-shim/es5-shim.min.js"></script>
        <script src="/node_modules/yate/lib/runtime.js"></script>
        <script src="/node_modules/noscript/dist/noscript.js"></script>
        <script src="/node_modules/noscript/yate/noscript-yate-externals.js"></script>
        <script src="/templates.yate.js"></script>
        <script src="/js/models.js"></script>
        <script src="/js/views.js"></script>
        <script src="/js/layouts.js"></script>
        <script src="/js/routes.js"></script>
        <script src="/js/app.js"></script>
    </head>
    <body class="page-body">
        apply . server-render-body
    </body>
    </html>
}

match .* server-render-body {
    <div id="app">
        <p style="margin: 3em 0; text-align: center;">
            'Загрузка...'
        </p>
    </div>
}

match .*[ .server-render ] server-render-body {
    apply .views.* ns-view

    <script>
    'var __nsInit = function() {{\n'
    for .server-render-models {
        'ns.Model.get(\'{ .id }\', { .paramsString }).setData({ .dataString });\n'
    }
    '}};'
    </script>
}

// ----------------------------------------------------------------------------------------------------------------- //

match .head ns-view-add-class {
    " island head"
}

match .head ns-view-content {
    "Шапка: она не меняется от страницы к странице"
}

match .index ns-view-add-class {
    " island"
}

// 1. Index page.
match .index ns-view-content {
    "Это индекс страница. На ней ничего нет."
    <br/>
    <br/>
    "Зато можно перейти к "
    <a href="{ ns-url('/photos') }">следующей странице</a>
    "."
}

// 1.1 Github github-profile

match .github-profile ns-view-add-class {
    " island"
}

match .github-profile ns-view-content {
    @style = "vertical-align: top;"
    profile = model('github-profile')
    <img src="{ profile.avatar_url }" width="60" style="display: inline-block; margin-right: 0.5em;" />
    <a href="{ profile.html_url }" style="vertical-align: top;">{ profile.login }</a>
}

// 2. Photo list page.

match .photos ns-view-add-class {
    " island"
}

match .photos ns-view-collection {
    "К примеру, вот фотки:"
    <br/>
    apply /.views.ns-view-collection-container.* ns-view
}

match .photos-item ns-view-add-class {
    " image-thumb"
}

match .photos-item ns-view-content {
    // Use helper when you need some model: model(id).
    photo = model('photo')

    <a href="{ ns-url('/photos/' + photo.id) }">
        <img src="{ photo.url_ }XXS"/>
    </a>
}

// 3. Single photo page.

match .photo-preview ns-view-add-class {
    " island"
}

match .photo-preview ns-view-content {
    "А вот и превью:"
    <br/>
    <br/>
    <img src="{ model('photo').url_ }L"/>
}
